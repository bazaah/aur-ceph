# $Id: PKGBUILD 287115 2017-01-22 01:54:44Z seblu $
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>

pkgname=ceph
pkgver=11.2.0
pkgrel=0.1
pkgdesc='Distributed, fault-tolerant storage platform delivering object, block, and file system'
arch=('x86_64')
url='https://ceph.com/'
license=('GPL')
makedepends=('boost' 'systemd' 'xfsprogs' 'python2-sphinx' 'python2-virtualenv'
  'cython2' 'cmake' 'yasm')
depends=('boost-libs' 'curl' 'expat' 'fcgi' 'fuse2' 'gcc-libs' 'glibc'
  'gperftools' 'keyutils' 'leveldb' 'libaio' 'libatomic_ops' 'libedit'
  'libsystemd' 'libutil-linux' 'ncurses' 'nss' 'python2' 'snappy'
  'python2-setuptools' 'xfsprogs')
options=('!emptydirs')
source=("https://download.ceph.com/tarballs/ceph-$pkgver.tar.gz"
        'ceph.sysusers'
        '14099.patch')
md5sums=('1e394a69820d71e5df19abe38ef647a6'
         'b3e24e3aa005a657ab475f84bfe3291a'
         '9d376f1f6d06be71c2cc6aa267a67869')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  cd $pkgname-$pkgver
  [[ -d build ]] || mkdir build
  cd build
  # list of options defaults: grep ^option CMakeLists.txt
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_SBINDIR=/usr/bin \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DSPHINX_BUILD=/usr/bin/sphinx-build2 \
    -DWITH_SYSTEM_BOOST=ON \
    -DWITH_SYSTEMD=ON \
    -DWITH_EMBEDDED=OFF \
    -DWITH_OPENLDAP=OFF \
    -DWITH_LTTNG=OFF \
    -DHAVE_BABELTRACE=OFF \
    -DWITH_TESTS=OFF \
    ..
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  # install tmpfiles.d
  install -Dm644 "$srcdir"/$pkgname-$pkgver/systemd/ceph.tmpfiles.d \
    usr/lib/tmpfiles.d/$pkgname.conf
  install -Dm644 "$srcdir"/ceph.sysusers \
    usr/lib/sysusers.d/$pkgname.conf

  # fix sbin path
  msg2 'Fix sbin paths'
  mv -v usr/sbin/* usr/bin

  # fix bash completions path
  msg2 'Fix bash completion path'
  install -d -m 755 usr/share/bash-completion
  mv -v etc/bash_completion.d usr/share/bash-completion/completions

  # remove debian init
  rm -v etc/init.d/ceph

  # fix python2 shebang, did not do it in prepare() because cmake remplace some
  msg2 'Fix python2 shebang'
  find usr/bin -type f -executable -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

# vim:set ts=2 sw=2 et:
